Imports System.ComponentModel.DataAnnotations
Imports System.ComponentModel.DataAnnotations.Schema
Imports Microsoft.EntityFrameworkCore
Imports Microsoft.EntityFrameworkCore.Migrations.Design
Imports Microsoft.EntityFrameworkCore.Migrations.Operations
Imports Xunit

Public Class VisualBasicMigrationsGeneratorTests
    <Fact>
    Sub FileExtension_works()
        Dim generator = CreateGenerator()

        Assert.Equal(".vb", generator.FileExtension)
    End Sub

    <Fact>
    Sub Language_works()
        Dim generator = CreateGenerator()

        Assert.Equal("VB", generator.Language)
    End Sub

    <Fact>
    Sub GenerateMigration_works()
        Dim generator = CreateGenerator()

        Dim result = generator.GenerateMigration(
            "MyNamespace",
            "MyMigration",
            New MigrationOperation() {},
            New MigrationOperation() {})

        Assert.NotNull(result)
    End Sub

    <Fact>
    Sub GenerateMetadata_works()
        Dim generator = CreateGenerator()
        Dim model = (New TestDbContext).Model

        Dim result = generator.GenerateMetadata(
            "MyNamespace",
            GetType(TestDbContext),
            "MyMigration",
            "00000000000000_MyMigration",
            model)

        Assert.NotNull(result)
    End Sub

    <Fact>
    Sub GenerateSnapshot_works()
        Dim generator = CreateGenerator()
        Dim model = (New TestDbContext).Model

        Dim result = generator.GenerateSnapshot(
            "MyNamespace",
            GetType(TestDbContext),
            "MyModelSnapshot",
            model)

        Assert.NotNull(result)

        Dim expectedCode =
"// <auto-generated />
Imports System
Imports Bricelam.EntityFrameworkCore.VisualBasic
Imports Microsoft.EntityFrameworkCore
Imports Microsoft.EntityFrameworkCore.Infrastructure
Imports Microsoft.EntityFrameworkCore.Metadata
Imports Microsoft.EntityFrameworkCore.Metadata.Internal
Imports Microsoft.EntityFrameworkCore.Migrations
Imports Microsoft.EntityFrameworkCore.Storage
Imports Microsoft.EntityFrameworkCore.Storage.Internal

Namespace MyNamespace
    <DbContext(GetType(VisualBasicMigrationsGeneratorTests.TestDbContext))>
    Partial Class MyModelSnapshot Inherits ModelSnapshot

        Protected Overrides Sub BuildModel(modelBuilder As ModelBuilder)
            modelBuilder
                .HasAnnotation(""ProductVersion"", ""2.1.0-preview1-28285"")
                .HasAnnotation(""Relational:Sequence:shared.OrderNumbers"", ""'OrderNumbers', 'shared', '1000', '5', '', '', 'Int32', 'False'"")
            modelBuilder.Entity(""Bricelam.EntityFrameworkCore.VisualBasic.VisualBasicMigrationsGeneratorTests+Customer"", Sub (b)

                    b.Property(Of Integer)(""Id"")
                        .ValueGeneratedOnAdd()

                    b.Property(Of String)(""FullName"")
                        .IsRequired()

                    b.HasKey(""Id"")

                    b.HasAlternateKey(""FullName"")
                        .HasName(""AK_Customer_FullName"")

                    b.ToTable(""Customers"")
                End Sub)

            modelBuilder.Entity(""Bricelam.EntityFrameworkCore.VisualBasic.VisualBasicMigrationsGeneratorTests+Order"", Sub (b)

                    b.Property(Of Integer)(""Year"")

                    b.Property(Of Integer)(""IdInYear"")

                    b.Property(Of Integer)(""CustomerId"")

                    b.Property(Of DateTime)(""OrderDate"")

                    b.Property(Of Decimal)(""TotalAmount"")

                    b.HasKey(""Year"", ""IdInYear"")

                    b.ToTable(""Orders"")
                End Sub)

            modelBuilder.Entity(""Bricelam.EntityFrameworkCore.VisualBasic.VisualBasicMigrationsGeneratorTests+OrderItem"", Sub (b)

                    b.Property(Of Integer)(""Id"")
                        .ValueGeneratedOnAdd()

                    b.Property(Of String)(""OrderItemProductId"")

                    b.Property(Of Integer?)(""ParentOrderIdInYear"")

                    b.Property(Of Integer?)(""ParentOrderYear"")

                    b.Property(Of Integer)(""Quantity"")

                    b.HasKey(""Id"")

                    b.HasIndex(""OrderItemProductId"")

                    b.HasIndex(""ParentOrderYear"", ""ParentOrderIdInYear"")

                    b.ToTable(""OrderItem"")
                End Sub)

            modelBuilder.Entity(""Bricelam.EntityFrameworkCore.VisualBasic.VisualBasicMigrationsGeneratorTests+Product"", Sub (b)

                    b.Property(Of String)(""Id"")
                        .ValueGeneratedOnAdd()

                    b.Property(Of String)(""Name"")

                    b.HasKey(""Id"")

                    b.ToTable(""Product"")
                End Sub)

            modelBuilder.Entity(""Bricelam.EntityFrameworkCore.VisualBasic.VisualBasicMigrationsGeneratorTests+OrderItem"", Function (b)

                    b.HasOne(""Bricelam.EntityFrameworkCore.VisualBasic.VisualBasicMigrationsGeneratorTests+Product"", ""OrderItemProduct"")
                        .WithMany()
                        .HasForeignKey(""OrderItemProductId"")
                    b.HasOne(""Bricelam.EntityFrameworkCore.VisualBasic.VisualBasicMigrationsGeneratorTests+Order"", ""ParentOrder"")
                        .WithMany(""OrderItems"")
                        .HasForeignKey(""ParentOrderYear"", ""ParentOrderIdInYear"")
                End Function)
        End Sub
    End Class
End Namespace
"

        Assert.Equal(expectedCode, result)

    End Sub

    Private Function CreateGenerator() As VisualBasicMigrationsGenerator
        Return New VisualBasicMigrationsGenerator(New MigrationsCodeGeneratorDependencies(), New VisualBasicMigrationsGeneratorDependencies(New VisualBasicHelper(), New VisualBasicMigrationOperationGenerator(New VisualBasicMigrationOperationGeneratorDependencies(New VisualBasicHelper())), New VisualBasicSnapshotGenerator(New VisualBasicSnapshotGeneratorDependencies(New VisualBasicHelper()))))
    End Function

    Private Class TestDbContext
        Inherits DbContext

        Protected Overrides Sub OnConfiguring(options As DbContextOptionsBuilder)
            options.UseSqlite("Data Source=:memory:")
        End Sub

        Protected Overrides Sub OnModelCreating(modelBuilder As ModelBuilder)
            MyBase.OnModelCreating(modelBuilder)

            modelBuilder.HasSequence(Of Integer)("OrderNumbers", schema:="shared") _
                        .StartsAt(1000) _
                        .IncrementsBy(5)

            modelBuilder.Entity(Of Customer) _
                        .HasAlternateKey(Function(c) c.FullName) _
                        .HasName("AK_Customer_FullName")

            modelBuilder.Entity(Of Order) _
                        .HasKey(Function(o) New With {o.Year, o.IdInYear})
        End Sub


        Public Property Customers As DbSet(Of Customer)
        Public Property Orders As DbSet(Of Order)

    End Class

    Private Class Customer
        Public Property Id As Integer
        Public Property FullName As String

    End Class

    Private Class Order
        Public Property Year As Integer
        Public Property IdInYear As Integer
        Public Property CustomerId As Integer
        Public Property TotalAmount As Decimal
        Public Property OrderDate As DateTime

        Public Property OrderItems As ICollection(Of OrderItem)
    End Class

    Private Class OrderItem
        Public Property Id As Integer
        Public Property ParentOrder As Order
        Public Property OrderItemProduct As Product
        Public Property Quantity As Integer

    End Class

    Private Class Product
        Public Property Id As String
        Public Property Name As String
    End Class
End Class